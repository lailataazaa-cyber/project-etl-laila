# terraform to create bucket in gcs
resource "google_storage_bucket" "bucket" {
  name          = "laila-etl-data"
  location      = "EU"
  force_destroy = true
}
# terraform to create dataset in bigquery
resource "google_bigquery_dataset" "dataset" {
  dataset_id    = "lailadata"
  friendly_name = "Retail Dataset"
  description   = "Retail Dataset"
  location      = "EU"
}
resource "google_workflows_workflow" "workflow" {
  name            = "retail-dsy-workflow"
  description     = "Retail Dataset Workflow"
  source_contents = local.workflow_yaml
  region          = var.region
  service_account = google_service_account.service_account.email
}
# terraform to create table in bigquery
resource "google_bigquery_table" "raw_country" {
  dataset_id = "lailadata"
  table_id   = "raw_country"
  schema     = <<EOF
  [
    {
      "name": "id",
      "type": "STRING"
    },
    {
      "name": "iso",
      "type": "STRING"
    },
    {
      "name": "name",
      "type": "STRING"
    },
    {
      "name": "nicename",
      "type": "STRING"
    },
    {
      "name": "iso3",
      "type": "STRING"
    },
    {
      "name": "numcode",
      "type": "STRING"
    },
    {
      "name": "phonecode",
      "type": "STRING"
    }]
  EOF
}
# terraform to create table raw_invoice  InvoiceNo,StockCode,Description,Quantity,InvoiceDate,UnitPrice,CustomerID,Country
resource "google_bigquery_table" "raw_invoice" {
  dataset_id = "lailadata"
  table_id   = "raw_invoice"
  schema     = <<EOF
  [
    {
      "name": "InvoiceNo",
      "type": "STRING"
    },
    {
      "name": "StockCode",
      "type": "STRING"
    },
    {
      "name": "Description",
      "type": "STRING"
    },
    {
      "name": "Quantity",
      "type": "STRING"
    },
    {
      "name": "InvoiceDate",
      "type": "STRING"
    },
    {
      "name": "UnitPrice",
      "type": "STRING"
    },
    {
      "name": "CustomerID",
      "type": "STRING"
    },
    {
      "name": "Country",
      "type": "STRING"
    }]
  EOF
}
# terraform to enable Identity and Access Management (IAM) API
resource "google_project_service" "service" {
  service = "iam.googleapis.com"
}


# terraform to enable Cloud Run API
resource "google_project_service" "cloud_run_api" {
  service = "run.googleapis.com"
}

# terraform to enable Cloud Build API
resource "google_project_service" "cloudbuild_api" {
  service = "cloudbuild.googleapis.com"
}

# terraform to enable Artifact Registry API
resource "google_project_service" "artifact_registry_api" {
  service = "artifactregistry.googleapis.com"
}

# terraform to create Artifact Registry Docker repository for dbt images
resource "google_artifact_registry_repository" "dbt_images" {
  location      = var.region
  repository_id = var.ar_repo_name
  description   = "dbt images"
  format        = "DOCKER"
}

# terraform to create service account
resource "google_service_account" "service_account" {
  account_id   = "retail-etl-sa"
  display_name = "Retail ETL SA"
}

# terraform to grant roles gcs reader to service account
resource "google_project_iam_member" "storage_object_viewer" {
  project = var.project_id
  role    = "roles/storage.objectViewer"
  member  = "serviceAccount:${google_service_account.service_account.email}"
}

# terraform to grant roles workflow invoker to service account
resource "google_project_iam_member" "workflow_invoker" {
  project = var.project_id
  role    = "roles/workflows.invoker"
  member  = "serviceAccount:${google_service_account.service_account.email}"
}

# terraform to grant roles eventarc admin to service account
resource "google_project_iam_member" "eventarc_admin" {
  project = var.project_id
  role    = "roles/eventarc.admin"
  member  = "serviceAccount:${google_service_account.service_account.email}"
}



# terraform to create Cloud Run Job to execute dbt with BigQuery
resource "google_cloud_run_v2_job" "dbt" {
  name     = var.dbt_job_name
  location = var.region

  template {
    template {
      service_account = google_service_account.service_account.email
      max_retries     = 1
      timeout         = "1800s"

      containers {
        image = var.dbt_image
        args  = ["run"]
      }
    }
  }
}

# terraform to grant Cloud Run Job runner to service account
resource "google_cloud_run_v2_job_iam_member" "run_job_runner" {
  name     = google_cloud_run_v2_job.dbt.name
  location = google_cloud_run_v2_job.dbt.location
  role     = "roles/run.invoker"
  member   = "serviceAccount:${google_service_account.service_account.email}"
}

# terraform to grant Cloud Run developer role to service account
resource "google_project_iam_member" "run_developer" {
  project = var.project_id
  role    = "roles/run.developer"
  member  = "serviceAccount:${google_service_account.service_account.email}"
}

resource "google_eventarc_trigger" "trigger" {
  name            = "retail-dsy-trigger"
  location        = "eu"
  service_account = google_service_account.service_account.email
  matching_criteria {
    attribute = "type"
    value     = "google.cloud.storage.object.v1.finalized"
  }
  matching_criteria {
    attribute = "bucket"
    value     = google_storage_bucket.bucket.name
  }

  destination {
    workflow = google_workflows_workflow.workflow.id
  }
}

# terraform to create Cloud Build trigger for all git branches
resource "google_cloudbuild_trigger" "terraform_all_branches" {
  name        = var.cloudbuild_trigger_name
  description = "Run Terraform pipeline on every branch push"

  github {
    owner = var.github_owner
    name  = var.github_repo_name
    push {
      branch = var.cloudbuild_trigger_branch_regex
    }
  }

  filename = "cloudbuild.yaml"

  substitutions = {
    _TF_STATE_BUCKET = var.tf_state_bucket
    _TF_STATE_PREFIX = var.tf_state_prefix
  }
}